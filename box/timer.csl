#ifndef BOX_TIMER_CSL
#define BOX_TIMER_CSL 1

#include <box/memory.csl>

#define _BX_TOKENPASTE(x, y) x ## y
#define _BX_TOKENPASTE2(x, y) _BX_TOKENPASTE(x, y)
#define _bxLbl _BX_TOKENPASTE2(__bxLbl, __LINE__)

#define _BX_TGRP(name) gBxTimerGroup##name

#define _BX_TGRP_STRIDE (3)
#define _bxTgrpMark(n, idx)     (llList2Float(_BX_TGRP(n),    idx*_BX_TGRP_STRIDE))
#define _bxTgrpId(n, idx)     (llList2Integer(_BX_TGRP(n), 1+(idx*_BX_TGRP_STRIDE)))
#define _bxTgrpInterval(n, idx) (llList2Float(_BX_TGRP(n), 2+(idx*_BX_TGRP_STRIDE)))
#define _bxTgrpHandle(n, idx) (llList2Integer(_BX_TGRP(n), 2+(idx*_BX_TGRP_STRIDE)))

#define BOX_TIMER_TYPE_ONCE     TYPE_INTEGER
#define BOX_TIMER_TYPE_INTERVAL TYPE_FLOAT
#define _bxTgrpType(n, idx) (llGetListEntryType(_BX_TGRP(n), 2+(idx*_BX_TGRP_STRIDE)))

/**
 * Get the number of active timers.
 * @param name void The name of the timer group created with BOX_TIMER_GROUP.
 */
#define bxTimerCount(name) (llGetListLength(_BX_TGRP(name)) / _BX_TGRP_STRIDE)

#define _BX_TDELTA(name) gBxTimerDelta##name

/**
 * Create a new timer group. Only one timer group should be in use at a time.
 * @param name void The unique name of this timer group.
 */
#define BOX_TIMER_GROUP(name)                                       \
float _BX_TDELTA(name);                                             \
list _BX_TGRP(name);                                                \
_bxTimerInsert##name(list parts)                                    \
{                                                                   \
    float mark = llList2Float(parts, 0);                            \
    integer __bxII;                                                 \
    for (__bxII = bxTimerCount(name) - 1; __bxII >= 0; __bxII--) {  \
         if (_bxTgrpMark(name, __bxII) <= mark) {                   \
            jump _bxLbl; }} @_bxLbl;                                \
    __bxII += 1;                                                    \
    _BX_TGRP(name) = llListInsertList(bxMove(_BX_TGRP(name)),       \
                                       parts,                       \
                                       __bxII*_BX_TGRP_STRIDE);     \
    if (0 == __bxII) {                                              \
        _BX_TDELTA(name) = mark;                                    \
        llSetTimerEvent(mark);                                      \
    }                                                               \
}                                                                   \
_bxTimerStart##name(integer id, float interval)                     \
{                                                                   \
    _bxTimerInsert##name([interval, id, interval]);                 \
}                                                                   \
integer _bxTimerOnce##name(integer id, float delay)                 \
{                                                                   \
    /* Generate a unique handle */                                  \
    integer h;                                                      \
    do {                                                            \
        h = (integer)llFrand(65536)|((integer)llFrand(65536)<<16);  \
        h = h | 0x80000000;                                         \
    } while (llListFindList(_BX_TGRP(name), [h]) != -1);            \
                                                                    \
    _bxTimerInsert##name([delay, id, h]);                           \
    return h;                                                       \
}                                                                   \
list _bxTimerTick##name()                                           \
{                                                                   \
    float now = _BX_TDELTA(name);                                   \
                                                                    \
    list expired;                                                   \
    integer ii;                                                     \
    for (ii = bxTimerCount(name) - 1; ii >= 0; ii--) {              \
        float mark = _bxTgrpMark(name, ii);                         \
        mark -= now;                                                \
        if (mark <= 0) {                                            \
            expired += llList2List(_BX_TGRP(name),                  \
                                   (ii*_BX_TGRP_STRIDE) + 1,        \
                                   (ii*_BX_TGRP_STRIDE) + 2);       \
        } else {                                                    \
            _BX_TGRP(name) =                                        \
                llListReplaceList(bxMove(_BX_TGRP(name)),           \
                                  [mark],                           \
                                  ii*_BX_TGRP_STRIDE,               \
                                  ii*_BX_TGRP_STRIDE);              \
        }                                                           \
    }                                                               \
                                                                    \
    /* Remove expired timers from the list */                       \
    integer n_expire = llGetListLength(expired)/2;                  \
    if (0 < n_expire) {                                             \
        _BX_TGRP(name) = llDeleteSubList(bxMove(_BX_TGRP(name)),    \
                                         0,                         \
                                         (n_expire*_BX_TGRP_STRIDE)-1);\
                                                                    \
        /* Re-add reoccurring timers */                             \
        for (ii = n_expire - 1; ii >= 0; ii--) {                    \
            integer type = llGetListEntryType(expired, (ii*2)+1);   \
            if (type == BOX_TIMER_TYPE_INTERVAL) {                  \
                float interval = llList2Float(expired, (ii*2)+1);   \
                _bxTimerInsert##name([interval,                     \
                                     llList2Integer(expired,(ii*2)),\
                                     interval]);                    \
            }                                                       \
        }                                                           \
    }                                                               \
                                                                    \
    /* Finally, stop the lsl timer if no pending events */          \
    if (bxTimerCount(name) == 0) {                                  \
        _BX_TDELTA(name) = 0.0;                                     \
        llSetTimerEvent(0.0);                                       \
    } else if (0 == n_expire) {                                     \
        _BX_TDELTA(name) = _bxTgrpMark(name, 0);                    \
        llSetTimerEvent(_BX_TDELTA(name));                          \
    }                                                               \
    return expired;                                                 \
}                                                                   \
integer _bxTimerStopOnce##name(integer handle)                      \
{                                                                   \
    integer ii;                                                     \
    for (ii = bxTimerCount(name) - 1; ii >= 0; ii--) {              \
        if (_bxTgrpType(name, ii) == BOX_TIMER_TYPE_ONCE) {         \
            if (_bxTgrpHandle(name, ii) == handle) {                \
                integer id = _bxTgrpId(name, ii);                   \
                _BX_TGRP(name) =                                    \
                    llDeleteSubList(bxMove(_BX_TGRP(name)),         \
                                    ii,                             \
                                    ii+_BX_TGRP_STRIDE-1);          \
                if (0 == ii) {                                      \
                    if (0 == bxTimerCount(name)) {                  \
                        _BX_TDELTA(name) = 0.0;                     \
                        llSetTimerEvent(0.0);                       \
                    }                                               \
                }                                                   \
                return id;                                          \
            }                                                       \
        }                                                           \
    }                                                               \
    return 0;                                                       \
}                                                                   \
integer _bxTimerStop##name(integer id, integer all)                 \
{                                                                   \
    integer ii = 0;                                                 \
    integer ocount = bxTimerCount(name);                            \
    integer count = ocount;                                         \
    for (ii = 0; ii < count; ii++) {                                \
        if (_bxTgrpId(name, ii) == id) {                            \
            _BX_TGRP(name) = llDeleteSubList(bxMove(_BX_TGRP(name)),\
                                             ii*_BX_TGRP_STRIDE,    \
                                             ((ii+1)*_BX_TGRP_STRIDE)-1);\
            if (ii == 0) {                                          \
                if (bxTimerCount(name) == 0) {                      \
                    _BX_TDELTA(name) = 0.0;                         \
                    llSetTimerEvent(0.0);                           \
                }                                                   \
            }                                                       \
                                                                    \
            if (!all) {                                             \
                return TRUE;                                        \
            }                                                       \
                                                                    \
            ii--;                                                   \
            count--;                                                \
        }                                                           \
    }                                                               \
    return count != ocount;                                         \
}

/**
 * Start a new reoccurring timer.
 * @param group void Name of timer group created with BOX_TIMER_GROUP.
 * @param id integer Identifier for this timer. Does not have to be unique.
 * @param interval float How long, in seconds, should elapse between events.
 */
#define bxTimerStart(group, id, interval) _bxTimerStart##group(id, interval)

/**
 * Schedule a one-time event to occur after some delay.
 * @param group void Name of the timer group created with BOX_TIMER_GROUP.
 * @param id integer Identifier for this timer. Does not have to be unique.
 * @param delay float How long, in seconds, before this event should occur.
 * @returns A key you can pass to bxTimerStopOnce to cancel a particular
 *          event.
 */
#define bxTimerOnce(group, id, delay) _bxTimerOnce##group(id, delay)

/**
 * Call from within a timer event. Returns a list of triggered timer events,
 * rescheduling reoccurring ones.
 *
 * @param group void Name of the timer group created with BOX_TIMER_GROUP.
 * @returns A list following the format [id, <interval or handle>, ...]
 */
#define bxTimerTick(group) _bxTimerTick##group()

/**
 * Stops the next event with the matching id. In the case of reoccurring events,
 * will stop reoccurrences as well. If you have two timers (i.e. two calls to
 * bxTimerStart) with the same id, only the next timer and its reoccurences will
 * be cancelled.
 * @param group void Name of the timer group created with BOX_TIMER_GROUP.
 * @param id integer Identifier for a timer given to bxTimerStart or bxTimerOnce.
 * @returns TRUE if a timer was stopped, FALSE otherwise.
 */
#define bxTimerStopNext(group, id) _bxTimerStop##group(id, FALSE)

/**
 * Stops the all timers with the matching id. In the case of reoccurring events,
 * will stop reoccurrences as well. If you have two timers (i.e. two calls to
 * bxTimerStart) with the same id, both timers and their reoccurences will
 * be cancelled.
 * @param group void Name of the timer group created with BOX_TIMER_GROUP.
 * @param id integer Identifier for a timer given to bxTimerStart or bxTimerOnce.
 * @returns TRUE if a timer was stopped, FALSE otherwise.
 */
#define bxTimerStopAll(group, id) _bxTimerStop##group(id, TRUE)

/**
 * Cancel an event created with bxTimerStartOnce.
 * @param group void Name of the timer group created with BOX_TIMER_GROUP.
 * @param handle integer A handle used to create a timer with bxTimerStartOnce.
 * @returns The id of the timer that was stopped, or zero if no matching timer
 *          was found.
 */
#define bxTimerStopOnce(group, handle)  _bxTimerStopOnce##group(handle)

#endif /* BOX_TIMER_CSL */
/* vim: set et sw=4 sts=4 ts=4: */
