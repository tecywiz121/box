#ifndef BOX_UTIL_CSL
#define BOX_UTIL_CSL 1

/**
 * Returns the current owner, or the avatar that deeded the object if group owned.
 * @returns The avatar that deeded the object to the group, or llGetOwner.
 */
key bxGetOwnerAvatar()
{
    list details = llGetObjectDetails(llGetKey(), [OBJECT_OWNER, OBJECT_LAST_OWNER_ID]);
    key owner = llList2Key(details, 0);
    if (owner != NULL_KEY) {
        return owner;
    } else {
        return llList2Key(details, 1);
    }
}

string bxList2Str(list in, string sep)
{
    string r;
    integer c;
    while (1 < (c = llGetListLength(in))) {
        r += llEscapeURL(llList2String(in, 0)) + sep;
        in = llDeleteSubList(in, 0, 0);
    }
    if (1 == c) {
        r += llEscapeURL(llList2String(in, 0));
    }
    return r;
}

list bxStr2List(string in, string sep)
{
    list r = llParseStringKeepNulls(in, [sep], []);
    integer ii;
    for (ii = llGetListLength(r)-1; ii >= 0; ii--) {
        r = llListReplaceList(r, [llUnescapeURL(llList2String(r, ii))], ii, ii);
    }
    return r;
}

#endif /* BOX_UTIL_CSL */
