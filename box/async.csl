#ifndef BOX_ASYNC_CSL
#define BOX_ASYNC_CSL 1

#include <box/memory.csl>
#include <box/timer.csl>
#include <box/util.csl>

/**
 * Add async message support to a timer group.
 * @param tgrp void A timer group created with BOX_TIMER.
 */
#define BOX_ASYNC(tgrp)                                                     \
integer _bxAsyncBegin##tgrp(integer type, float timeout, integer owner_only)\
{                                                                           \
    /* Restrict type to 0-0x3FFFFFFF */                                     \
    type = 0x3FFFFFFF & type;                                               \
    type = 0x40000000 | type;                                               \
                                                                            \
    key sender = NULL_KEY;                                                  \
    if (owner_only) {                                                       \
        type = type | 0x80000000;                                           \
        sender = bxGetOwnerAvatar();                                        \
    }                                                                       \
                                                                            \
    /* Start expiry timer */                                                \
    integer channel = bxTimerOnce(tgrp, type, timeout);                     \
                                                                            \
    /* Activate a listen on the channel */                                  \
    llListen(channel, "", sender, "");                                      \
    return channel;                                                         \
}                                                                           \
integer _bxAsyncFinishing##tgrp(integer channel, key sender)                \
{                                                                           \
    /* Find our timer */                                                    \
    integer id = bxTimerGetId(tgrp, channel);                               \
    if (0 == id) {                                                          \
        return FALSE;                                                       \
    }                                                                       \
                                                                            \
    /* Check if we're an owner only listen */                               \
    if (id & 0x80000000) {                                                  \
        id = id & 0x7FFFFFFF;                                               \
        if (sender != bxGetOwnerAvatar()) {                                 \
            return FALSE;                                                   \
        }                                                                   \
    }                                                                       \
                                                                            \
    /* Make sure this is actually an async timer */                         \
    if (id < 0x40000000) {                                                  \
        return FALSE;                                                       \
    }                                                                       \
                                                                            \
    return 0x3FFFFFFF & id;                                                 \
}                                                                           \
_bxAsyncFinish##tgrp(integer channel)                                       \
{                                                                           \
    integer id;                                                             \
    if (0 != (id = bxTimerStopOnce(tgrp, channel))) {                       \
        key sender = NULL_KEY;                                              \
        if (id & 0x80000000) {                                              \
            sender = bxGetOwnerAvatar();                                    \
        }                                                                   \
        /* Get original handle by calling llListen with same arguments */   \
        llListenRemove(llListen(channel, "", sender, ""));                  \
    }                                                                       \
}                                                                           \
list _bxAsyncExpire##tgrp(integer id, integer handle)                       \
{                                                                           \
    key sender = NULL_KEY;                                                  \
    if (id & 0x80000000) {                                                  \
        sender = bxGetOwnerAvatar();                                        \
        id = id & 0x7FFFFFFF;                                               \
    }                                                                       \
    if (id < 0x40000000) {                                                  \
        return [];                                                          \
    }                                                                       \
    /* Get back original handle by calling llListen with same arguments */  \
    llListenRemove(llListen(handle, "", sender, ""));                       \
    return [handle, 0x3FFFFFFF & id];                                       \
}

/**
 * Begins a new async request.
 * @param tgrp void The tgrp of the timer group created with BOX_TIMER_GROUP.
 * @param type integer A number that uniquely identifies the 'type' of the
 *                     request. Will be returned when the event finishes or
 *                     expires. Must be in range 1-2047 inclusive.
 * @param timeout float How many seconds to wait before giving up on the request.
 * @param owner_only integer If TRUE, will only accept messages from llGetOwner.
 * @returns The channel number assigned to this request.
 */
#define bxAsyncBegin(tgrp, type, timeout, owner_only) _bxAsyncBegin##tgrp(type, timeout, owner_only)

/**
 * Check for a completed outstanding request on the given channel.
 * @param tgrp void The name of the timer group created with BOX_TIMER_GROUP.
 * @param channel integer The channel parameter from the listen event.
 * @param sender key The UUID of the message sender.
 * @returns The type of the request started with bxAsyncBegin, or 0 if no
 *          request matches.
 */
#define bxAsyncFinishing(tgrp, channel, sender) _bxAsyncFinishing##tgrp(channel, sender)

/**
 * Remove an async timer and listen.
 * @param tgrp void The tgrp of the timer group created with BOX_TIMER_GROUP.
 * @param channel integer The channel parameter from the listen event.
 */
#define bxAsyncFinish(tgrp, channel) _bxAsyncFinish##tgrp(channel)

/**
 * Check for any expired outstanding requests.
 * @param name void The name of the timer group created with BOX_TIMER_GROUP.
 * @param id integer The id as returned by bxTimerTick.
 * @param handle integer The handle as returned by bxTimerTick.
 * @returns A strided list of [channel, type]
 */
#define bxAsyncExpire(tgrp, id, handle) _bxAsyncExpire##tgrp(id, handle)

/**
 * Shortcut for bxAsyncExpire from inside of BOX_TIMER_FOR_EACH.
 * @param name void The name of the timer group created with BOX_TIMER_GROUP.
 * @returns A strided list of [channel, type]
 */
#define bxAsyncExpireEvent(tgrp) bxAsyncExpire(tgrp, bxTimerEventId(tgrp), bxTimerEventHandle(tgrp))

#endif /* BOX_ASYNC_CSL */
